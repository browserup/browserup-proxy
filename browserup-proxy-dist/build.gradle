/*
 * Modifications Copyright (c) 2019 BrowserUp, Inc.
 */

plugins {
    id 'application'
    id 'distribution'
}

archivesBaseName = 'browserup-proxy-dist'

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }

            pom.project {
                name 'browserup-proxy-dist'
                description 'BrowserUp Proxy - Dist'
                url 'https://github.com/browserup/browserup-proxy'
                packaging 'jar'

                scm {
                    connection 'scm:git:git@github.com:browserup/browserup-proxy.git'
                    developerConnection 'scm:git:git@github.com:browserup/browserup-proxy.git'
                    url 'https://github.com/browserup/browserup-proxy/tree/master/browserup-proxy-dist'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'browserup'
                        name 'BrowserUp, Inc.'
                        email 'developers@browserup.com'
                    }
                }
            }
        }
    }
}

distributions {
    custom {
        contents {
            from '../LICENSE'
            from '../README.md'
            from '../CHANGELOG.md'
            from('../browserup-proxy-core/build/libs') {
                include "*-sources.jar"
                include "*-javadoc.jar"
            }
            from('../browserup-proxy-rest/build/libs') {
                include "*-sources.jar"
                include "*-javadoc.jar"
            }
            from('../browserup-proxy-rest/build/libs') {
                include "*-sources.jar"
                include "*-javadoc.jar"
            }
            from ('../browserup-proxy-core/src/main/resources/sslSupport') {
                into 'ssl-support'
            }
        }
    }
}

dependencies {
    implementation project(':browserup-proxy-core')
    implementation project(':browserup-proxy-rest')

    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.8'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.8'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.8'
    implementation 'com.google.inject:guice:4.2.2'
    implementation 'com.google.inject.extensions:guice-servlet:4.2.2'
    implementation 'com.google.inject.extensions:guice-multibindings:4.2.2'
    implementation 'com.google.sitebricks:sitebricks:0.8.11'
    implementation 'org.apache.logging.log4j:log4j-api:2.11.2'
    implementation 'org.apache.logging.log4j:log4j-core:2.11.2'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.2'
    implementation 'org.eclipse.jetty:jetty-server:9.4.18.v20190429'
    implementation 'org.eclipse.jetty:jetty-servlet:9.4.18.v20190429'
    implementation 'org.javassist:javassist:3.25.0-GA'
}

mainClassName = 'com.browserup.bup.proxy.Main'
